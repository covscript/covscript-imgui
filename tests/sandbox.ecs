import imgui.*

# Init ImGUI
system.file.remove("./imgui.ini")
#var app=fullscreen_application(0,"CovScript ImGUI Sandbox")
var app=window_application(0.75*imgui.get_monitor_width(0),0.75*imgui.get_monitor_height(0),"CovScript ImGUI Sandbox")
style_color_light()

# Basic Variables
var window_opened=true,
    col=vec4(0.5,0.5,0.5,1),
    radius=10,
    margin=20

# Constants
constant
    gravity=10,
    air_drop=1,
    delta_time=0.1,
    mass=0.5,
    size=0.01

# Removal of static balls
var remove_static_balls=false,
    remove_speed_threshold=1,
    remove_attitude_threshold=radius

class ball_entity
    var col=null
    var speed_x=math.rand(40,80)
    var speed_y=0
    var posit_x=margin+radius
    var posit_y=margin+radius
    function run()
        if posit_x<=get_window_pos_x()+margin+radius
            if posit_x<get_window_pos_x()
                speed_x=get_mouse_drag_delta_x()
            else
                speed_x=math.abs(speed_x)
            end
            posit_x=get_window_pos_x()+margin+radius
        end
        if posit_x>=get_window_pos_x()+get_window_width()-margin-radius
            if posit_x>get_window_pos_x()+get_window_width()
                speed_x=-get_mouse_drag_delta_x()
            else
                speed_x=-math.abs(speed_x)
            end
            posit_x=get_window_pos_x()+get_window_width()-margin-radius
        end
        if posit_y<=get_window_pos_y()+margin+radius
            if posit_y<get_window_pos_y()
                speed_y=get_mouse_drag_delta_y()
            else
                speed_y=math.abs(speed_y)
            end
            posit_y=get_window_pos_y()+margin+radius
        end
        if posit_y>=get_window_pos_y()+get_window_height()-margin-radius
            if posit_y>get_window_pos_y()+get_window_height()
                speed_y=-get_mouse_drag_delta_y()
            else
                speed_y=-math.abs(speed_y)
            end
            posit_y=get_window_pos_y()+get_window_height()-margin-radius
        end
        var ax=-(air_drop*size*speed_x)/mass
        var ay=(mass*gravity-2*air_drop*size*speed_y)/mass
        posit_x=posit_x+speed_x*delta_time+0.5*ax*delta_time^2
        posit_y=posit_y+speed_y*delta_time+0.5*ay*delta_time^2
        speed_x=speed_x+ax*delta_time
        speed_y=speed_y+ay*delta_time
        add_circle_filled(vec2(posit_x,posit_y),radius,col,360)
    end
end

var ball_list=new list

while !app.is_closed()
    app.prepare()
    begin_window("Sandbox",window_opened,{flags.no_collapse,flags.no_resize})
        if !window_opened
            break
        end
        set_window_size(vec2(app.get_window_width()/2,app.get_window_height()/2))
        if tree_node("Property")
            color_edit4("Ball Color",col)
            if button("Add a ball")
                var ball=new ball_entity
                ball.col=col
                ball.posit_x+=get_window_pos_x()
                ball.posit_y+=get_window_pos_y()
                ball_list.push_back(move(ball))
            end
            same_line()
            if button("Reset")
                ball_list.clear()
            end
            same_line()
            text("Ball count: "+ball_list.size)
            check_box("Remove static balls", remove_static_balls)
            tree_pop()
        end
        if remove_static_balls
            for it=ball_list.begin; it!=ball_list.end; null
                if it.data.posit_y>get_window_pos_y()+get_window_height()-margin-radius-remove_attitude_threshold &&
                    math.abs(it.data.speed_x)<remove_speed_threshold &&
                        math.abs(it.data.speed_y)<remove_speed_threshold
                    it=ball_list.erase(it)
                else
                    it.data.run()
                    it.next()
                end
            end
        else
            foreach it in ball_list do it.run()
        end
    end_window()
    app.render()
end